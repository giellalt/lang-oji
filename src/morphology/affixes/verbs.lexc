!todo: VII, VTI (all). VTA themes, VAI fine tuning, de-nasalize conjunct order. Unspecified actor needs careful thought. Check features for consistency
!! Verb inflection
!! ---------------
!! The UNDEFINED language verbs inflect in persons.

! generated template: person-number, number in lower case
! generated root.lexc: number-person, number in camel case
!generated assumes zero conversion?
!#:0   Nouns   ;

!#####
!#Bibliography info
!#####

!Not yet entered

!#####
!#Commence work
!#####

!Need to make sure features match between yaml and here

!does all verbal inflection need to be in this file, or can I split it up a bit?

!Continuation lexicon after PREFIXES is in ../stems/
!This almost certainly is going to be revised
!../stems/verbs.lexc will have internal divisions
!furthermore, why shouldn't person prefixes be common between nouns and verbs?

!#####
!#Prefixes
!#####

!I can't remember if this needs to be specified first in the file
!suspect not ...
!but it is good form

LEXICON V_PREFIXES
1+:ni   V_FIRST_PERSON_FLAG ;
2+:gi   V_SECOND_PERSON_FLAG ;
3+:o    V_THIRD_PERSON_FLAG ;
        VERBS ; !intended ... no prefix, just go to verbs
!Add alignment recs to style

!style question: PERSON spelled out here and in flags
!but not some sublexicon titles have PERS

LEXICON V_FIRST_PERSON_FLAG !add _FLAG to style
@P.Person.1@  VERBS ;

LEXICON V_SECOND_PERSON_FLAG 
@P.Person.2@  VERBS ;

LEXICON V_THIRD_PERSON_FLAG 
@P.Person.3@  VERBS ;

!#####
!#VAI Morphological sub-classes
!#####

!looking like gateway but not labeled as such, stems will have to be edited too

LEXICON VAI_ORDERS 
            VAI_INDEP_NEG ; !not specifying +Indep
@D.Person@    VAI_NON_INDEP ;

LEXICON VAI_NON_INDEP 
+Conj:0     VAI_CONJ_NEG ;
+Imper:0    VAI_IMPER ;

!Independent order
LEXICON VAI_INDEP_NEG 
+Neg:sii    VAI_INDEP_PERSNUM ;
            VAI_INDEP_PERSNUM ;!intended ... no suffix, just go to persnum

LEXICON VAI_INDEP_PERSNUM !does this count as a gateway??
!<"+"[1|2]Sg>       VAI_INDEP_MOOD ; !Good move? do we want to specify the interpretation of null morphs?
@R.Person.2@  VAI_INDEP_LOCALNUM ;
@R.Person.1@  VAI_INDEP_LOCALNUM ;
+3:w        VAI_INDEP_THIRD_PERSON_FLAG ;
+X:m        VAI_INDEP_THIRD_PERSON_FLAG ;
            VAI_INDEP_MOOD ; !intended ... no suffix (first and second person singulars, viz above), just go to mood

LEXICON VAI_INDEP_LOCALNUM !style on linearizing nested sublexica? does this count as a gateway?
<["+1Pl"|"+21"] .x. [m i n]>   VAI_INDEP_MOOD ; !Good move? do we want to specify a particular value or let the combination of prefix and suffix do the work?
@R.Person.2@          VAI_INDEP_LOCALNUM_2PL ;

LEXICON VAI_INDEP_LOCALNUM_2PL 
+2Pl:m   VAI_INDEP_MOOD ;

LEXICON VAI_INDEP_THIRD_PERSON_FLAG 
@P.Person.3@  VAI_INDEP_MOOD ;

LEXICON VAI_INDEP_MOOD 
+Pret:ban       VAI_INDEP_PERS_GATEWAY ;
+Dub:dig        VAI_INDEP_PERS_GATEWAY ; 
+Pret+Dub:goban VAI_INDEP_PERS_GATEWAY ;
                VAI_INDEP_PERS_GATEWAY ; !intended ... no suffix, just go to gateway

LEXICON VAI_INDEP_PERS_GATEWAY !add GATEWAY to style
@D.Person.3@  # ; !should ending be an option earlier? we currently have two sublexica that can be exited with epsilon transitions before this ending
@R.Person.3@  VAI_INDEP_NONLOCNUM ;

LEXICON VAI_INDEP_NONLOCNUM 
+3Obv:an    # ;
+3Pl:ag     # ;
            # ;

!Conjunct order
!See Valentine  2001:222, 236-237
!assuming nasal final stems to avoid all hiatus repairs
!ibid 236-237 lacks nasal-final examples

!Insert schematic template here (also update style guide)

LEXICON VAI_CONJ_NEG 
+Neg:ziw        VAI_CONJ_PERSNUM ; !nas morph
!is this next legal? will it make incorrect zigwaawaad or ziwaad?
+Neg+3Pl:zigwaa VAI_CONJ_PERSNUM ; !nas morph, neg 3pl diff from pos 3pl
                VAI_CONJ_PERSNUM ;

!need to be able to adjust the [w]

LEXICON VAI_CONJ_PERSNUM 
!adjustments for context needed once move past nas
+1Sg:aanh      VAI_CONJ_PRET ; 
+2Sg:in        VAI_CONJ_PRET ; 
+3Sg:g         VAI_CONJ_PRET ; !nas morph
+1Pl:aang      VAI_CONJ_PRET ;
+2Pl:ing       VAI_CONJ_PRET ;
+21:eg         VAI_CONJ_PRET ;
+3Pl:waad      VAI_CONJ_PRET ;
+3Obv:inid     VAI_CONJ_PRET ; !nas morph
+X:ang         VAI_CONJ_PRET ; !nas morph

!need to be able to end or move on ...
!doesn't seem right to delay decision to end until the next sublexicon

LEXICON VAI_CONJ_PRET 
+Pret:ban   VAI_CONJ_DUB ;
            VAI_CONJ_DUB ;

LEXICON VAI_CONJ_DUB 
+Dub:en # ;
        # ;

!Imperative order
!Insert schematic template here (also update style guide)

LEXICON VAI_IMPER 
+Imm+2Sg:n       # ;
+Imm+2Pl:g       # ;
+Imm+21:daa      # ;
+Del+2Sg:kan     # ;
+Del+2Pl:kag     # ;
+Prob+2Sg:ken    # ;
+Prob+2Pl:kegon  # ;
+Prob+21:siidaa  # ;

!#####
!# VII Morphological sub-classes
!#####

!LEXICON VII_ORDERS 
!+Indep ;
!+Conj ;
!+Imper ;

!#####
!#Transitive Verb Morphological sub-classes
!#####

!as with VAI's much of this will probably be transferred to VERBS
LEXICON TRANS_INDEP 
+VTA+Indep:0        VTA_INDEP_THEME_GATEWAY ;
+VTI+Indep:0        VTI_INDEP ;!name needs to be followed by morpheme slot

LEXICON TRANS_NON_INDEP 
# ;
!+VTA+Conj:0        VTA_CONJ ; !name needs to be followed by morpheme slot
!+VTA+Imper:0       VTA_IMPER ; !name needs to be followed by morpheme slot
!+VTI+Conj:0        VTI_CONJ ;!name needs to be followed by morpheme slot
!+VTI+Imper:0       VTI_IMPER ;!name needs to be followed by morpheme slot

!#####
!#VTA Inflection
!#####

!VTA Independent order Valentine (2001):274
!Insert schematic template here (also update style guide)

LEXICON VTA_INDEP_THEME_GATEWAY 
@R.Person.2@  VTA_INDEP_THEME_SECOND_PERSON ;
@R.Person.1@  VTA_INDEP_THEME_FIRST_PERSON ;
@R.Person.3@  VTA_INDEP_THEME_THIRD_PERSON ;
@D.Person@    VTA_INDEP_THEME_THIRD_PERSON ; !danger, entry to indep currently requires person

!A chart of where you can find feature combinations:
!is 0 needed?
!Sub Feats:         1   2   3    3Obv   0   X
!Obj Feats:
!           1           1   2    2      2   2 
!           2       1       1    1      1   1
!           3       2   1        3      3   3 
!           3Obv    2   1   3           3   3

LEXICON VTA_INDEP_THEME_SECOND_PERSON !1
+2Sub+1Obj:i          VTA_INDEP_LOW_PERSON_1_FLAG ; !local direct
+1Sub+2Obj:ini        VTA_INDEP_LOW_PERSON_1_FLAG ; !local inverse
!long name here..., only case in theme sign where we have to track number
+1PlSub+2Obj:igoo        VTA_INDEP_LOW_PERSON_1_FLAG ; !local inverse
+2Sub+3Obj:aa         VTA_INDEP_LOW_PERSON_3_FLAG ; !non-local direct
+2Sub+3ObvObj:aa      VTA_INDEP_LOW_PERSON_3_FLAG ; !non-local direct
+3Sub+2Obj:igo        VTA_INDEP_LOW_PERSON_3_FLAG ; !non-local inverse
+3ObvSub+2Obj:igo     VTA_INDEP_LOW_PERSON_3_FLAG ; !non-local inverse
+0Sub+2Obj:igo        VTA_INDEP_LOW_PERSON_0_FLAG ; !non-local inverse
+XSub+2Obj:igoo       VTA_INDEP_LOW_PERSON_X_FLAG ; !passive-y
!obviatives behave just like singulars here...
!this just calls for a regex

LEXICON VTA_INDEP_THEME_FIRST_PERSON !2
+1Sub+3Obj:aa          VTA_INDEP_LOW_PERSON_3_FLAG ; !non-local direct
+1Sub+3ObvObj:aa       VTA_INDEP_LOW_PERSON_3_FLAG ; !non-local direct
+3Sub+1Obj:igo         VTA_INDEP_LOW_PERSON_3_FLAG ; !non-local inverse
+3ObvSub+1Obj:igo      VTA_INDEP_LOW_PERSON_3_FLAG ; !non-local inverse
+0Sub+1Obj:igo         VTA_INDEP_LOW_PERSON_0_FLAG ; !non-local inverse
+XSub+1Obj:igoo        VTA_INDEP_LOW_PERSON_X_FLAG ; !passive-y
!obviatives behave just like singulars here...
!this just calls for a regex

LEXICON VTA_INDEP_THEME_THIRD_PERSON !3
+3Sub+3ObvObj:aa       VTA_INDEP_LOW_PERSON_3_FLAG ; !non-local direct
+3ObvSub+3Obj:igo      VTA_INDEP_LOW_PERSON_3_FLAG ; !non-local inverse
+0Sub+3Obj:igo         VTA_INDEP_LOW_PERSON_0_FLAG ; !non-local inverse
+0Sub+3ObvObj:igo      VTA_INDEP_LOW_PERSON_0_FLAG ; !non-local direct
+XSub+3Obj:aa          VTA_INDEP_LOW_PERSON_X_FLAG ; !passive-y
+XSub+3ObvObj:aa       VTA_INDEP_LOW_PERSON_X_FLAG ; !non-local direct
!this just calls for a regex

LEXICON VTA_INDEP_LOW_PERSON_1_FLAG 
@P.LowPerson.1@     VTA_INDEP_NEG ;

LEXICON VTA_INDEP_LOW_PERSON_3_FLAG 
@P.LowPerson.3@     VTA_INDEP_NEG ;

LEXICON VTA_INDEP_LOW_PERSON_0_FLAG 
@P.LowPerson.0@     VTA_INDEP_NEG ; 

LEXICON VTA_INDEP_LOW_PERSON_X_FLAG 
@P.LowPerson.X@     VTA_INDEP_NEG ;

!!NOT DONE!!! need non-local inverse
!!would be nice to just have +SAP, +NSAP, +Dir, +Inv features ... but that might be untidy notation
!!@R.Person.2@+Dir+SAP:0i  VTA_INDEP_NEG ; !1 goal
!!@R.Person.2@+Inv+SAP:0in VTA_INDEP_NEG ; !2 goal 
!!+Dir+NSAP:0aa           VTA_INDEP_NEG ; !3 goal
!!+Inv+NSAP:0igo           VTA_INDEP_NEG ; !... (Valentine:274 provides igw)
!!@R.Person@@D.Person.3@+XSub:0igoo    VTA_INDEP_NEG ; !this should be ok 
!
!!could make features shorter by relying on position to define subject v object features (sub first, obj sec)
!@R.Person.2@+2Sub+1Obj:0i  VTA_INDEP_NEG ; !1 goal
!@R.Person.2@+1Sub+2Obj:0in VTA_INDEP_NEG ; !2 goal 
!!not sure if multiline regex is kosher
!<
![["+"[2|1|X]Sub"+"3[Pl|Obv|Sg]Obj]| !danger ... entry to TRANS_INDEP currently requires a person prefix ... lacking on +X!
!["+"3[Sg|Pl]Sub"+"3ObvObj]]
!>:aa                      VTA_INDEP_NEG ; !1,2,X -> 3, 3Sg, 3Pl -> 3Obv
!@R.Person@@D.Person.3@+XSub:0igoo    VTA_INDEP_NEG ; !this should be ok, except we probably should provide object tags for ease of use?

!negative morpheme  consistent across classes, but making it common would require a lot of flags

LEXICON VTA_INDEP_NEG 
+Neg:sii        VTA_INDEP_PERSNUM_GATEWAY ;
@R.LowPerson.0@   VTA_INDEP_INAN_SUB ;
@D.LowPerson.0@     VTA_INDEP_PERSNUM_GATEWAY ;

!Persnum slot is underdetermined by Valentine 2001:274
!duplicated in gt-norm-yamls/OJI_VTA_YAML_NOTES.txt
!Going through p 287
!axes reversed
!sub-dialects ignored
!codes: x: non-existent combination for this
!       -: no suffix in this slot
!       *: suffix belongs to later slot (NONLUCNUM or inan sub)
!Subject:           1Sg     1Pl         21          2Sg     2Pl     3Sg     3Obv    3Pl       0Sg           0Pl         X
!Object:    1Sg     x       x           x           -       m       -       -       og*       n*            nan*        -
!           1Pl     -       x           x           min     min     naan    naan    naan-ig*  naan          naan-in*    min
!           21      -       x           x           x       x       naan    naan    naan-ig*  naan          naan-in*    min
!           2Sg     -       -           x           x       x       -       -       og*       n*            nan*        -
!           2Pl     m       m           x           x       x       waa     waa?    waa-g*    naawaa        naawaa-n*   m
!           3Sg     -       naan        naan        -       waa     x       an*     x         n*            nan*        -
!           3Obv    -       naan        naan        -       waa     n*      x       waa-n*    n*            nan*        wan*
!           3Pl     g*      naan-ig*    naan-ig*    g*      waa-g*  x       waa-n*  x         naawaa        naawaa-n*   wag*
!Analysis
!m      1Sgx2Pl,    2Plx1Sg,    Xx2Pl   1Plx2Pl         Prefixes:2      2pl if first person present, or X, overridden by next
!min    2Sgx1Pl     2Plx1Pl     Xx1Pl   Xx21            Prefixes:2, 1   1Pl object of 2 or X (includes Xx21
!naan   [0|3]*x[1Pl|21]     [1Pl|21]x[3*]               Prefixes:2, 1   1Pl/21 whenever 3rd person
!waa    3[Sg|Obv]x[2|3]Pl   2Plx3*, 3Plx[3Obv|2Pl]      Prefixes:2, 3   2/3Pl whenever 3rd person (careful) 
!           (3sgx3pl impossible)
!naawaa 0*x[2|3]Pl                                      Prefixes:2, 3   2/3Pl obj  whenever inanimate sub
!                                                                        n-aawaa
!1pl subjects acting on 2nd persons trigger theme sign igoo

LEXICON VTA_INDEP_INAN_SUB 
+0Sub:n     VTA_INDEP_PERSNUM_GATEWAY ;
!need to adjust n-n sequences with n-naan

LEXICON VTA_INDEP_PERSNUM_GATEWAY 
!precise distribution fairly complex (Valentine (2001):274)
@R.Person.2@@R.LowPerson.1@   VTA_INDEP_PERSNUM_2X1 ;
@R.Person.2@@R.LowPerson.X@   VTA_INDEP_PERSNUM_2XX ;
@R.Person.2@@R.LowPerson.3@   VTA_INDEP_PERSNUM_2X3 ;
@R.Person.2@@R.LowPerson.0@   VTA_INDEP_PERSNUM_2X0 ;

@R.Person.1@@R.LowPerson.X@   VTA_INDEP_PERSNUM_1XX ;
@R.Person.1@@R.LowPerson.3@   VTA_INDEP_PERSNUM_1X3 ;
@R.Person.1@@R.LowPerson.0@   VTA_INDEP_PERSNUM_1X0 ;

@R.Person.3@                  VTA_INDEP_PERSNUM_3 ;
!optionality handled in daughters instead of in gateway

LEXICON VTA_INDEP_PERSNUM_2X1 
<[["+1Sg"|"+1Pl"]"+2Pl"] .x. [m]>      VTA_INDEP_MOOD ; 
+2Pl+1Sg:m              VTA_INDEP_MOOD ;
<[["+2Pl"|"+2Sg"]"+1Pl"] .x. [m i n]>   VTA_INDEP_MOOD ;
                        VTA_INDEP_MOOD ;

LEXICON VTA_INDEP_PERSNUM_2XX 
+X+2Pl:m    VTA_INDEP_MOOD ;
+X+21:min   VTA_INDEP_MOOD ;
            VTA_INDEP_MOOD ;

LEXICON VTA_INDEP_PERSNUM_2X3 
!strongly favor all caps in features...with CamelCase errors are too easy to make and miss
<[["+3Sg"|"+3Obv"|"+3Pl"]"+21"] .x. [n a a n]>   VTA_INDEP_MOOD ;
<["+21"["+3Sg"|"+3Obv"|"+3Pl"]] .x. [n a a n]>   VTA_INDEP_MOOD ;
<[["+3Sg"|"+3Obv"|"+3Pl"]"+2Pl"] .x. [w a a]>   VTA_INDEP_MOOD ;
<["+2Pl"["+3Sg"|"+3Obv"|"+3Pl"]] .x. [w a a]>   VTA_INDEP_MOOD ;
                            VTA_INDEP_MOOD ;

LEXICON VTA_INDEP_PERSNUM_2X0
<[["+0Sg"|"+0Pl"]"+2Pl"] .x. [a a w a a]> VTA_INDEP_MOOD ;
<[["+0Sg"|"+0Pl"]"+21"] .x. [n a a n]>   VTA_INDEP_MOOD ;
                        VTA_INDEP_MOOD ;

LEXICON VTA_INDEP_PERSNUM_1XX 
+X+1Pl:min  VTA_INDEP_MOOD ;
            VTA_INDEP_MOOD ;

LEXICON VTA_INDEP_PERSNUM_1X3 
<[["+3Sg"|"+3Obv"|"+3Pl"]"+1Pl"] .x. [n a a n]>  VTA_INDEP_MOOD ;
<["+1Pl"["+3Sg"|"+3Obv"|"+3Pl"]] .x. [n a a n]>  VTA_INDEP_MOOD ;
                            VTA_INDEP_MOOD ;

LEXICON VTA_INDEP_PERSNUM_1X0 
<[["+0Sg"|"+0Pl"]["+1Pl"|"+21"]] .x. [n a a n]>   VTA_INDEP_MOOD ;
                                VTA_INDEP_MOOD ;

LEXICON VTA_INDEP_PERSNUM_3 
+3Pl+3Obv:waa   VTA_INDEP_MOOD ;
+3Obv+3pl:waa   VTA_INDEP_MOOD ;
<[["+0Pl"|"+0Sg"]"+3Pl"] .x. [a a w a a]>   VTA_INDEP_MOOD ;
                VTA_INDEP_MOOD ;

LEXICON VTA_INDEP_MOOD 
+Pret:ban   VTA_INDEP_PERS_GATEWAY ;
+Dub:dig    VTA_INDEP_PERS_GATEWAY ;
            VTA_INDEP_PERS_GATEWAY ;  

LEXICON VTA_INDEP_PERS_GATEWAY !add GATEWAY to style
!Might need to be revised
@D.Person.3@ # ; !should ending be an option earlier? we currently have two sublexica that can be exited with epsilon transitions before this ending
@R.Person.3@ VTA_INDEP_NONLOCNUM ;

LEXICON VTA_INDEP_NONLOCNUM 
!precise distribution fairly complex (Valentine (2001):274)
+3Obv:an    # ;
+3Pl:ag     # ;

LEXICON VTI_INDEP !name needs to be followed with a morpheme slot
# ;

