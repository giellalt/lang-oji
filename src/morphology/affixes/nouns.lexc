!! Noun inflection
!! ---------------
!! The UNDEFINED language nouns inflect in cases.

LEXICON N_PREFIXES 
1+:ni N_FIRST_PERSON_FLAG ;
2+:gi N_SECOND_PERSON_FLAG NOUNS ;
3+:o  N_THIRD_PERSON_FLAG ;
X+:i  N_X_PERSON_FLAG ; !will need to disallow indefinite possession on alienable
        NOUNS ;
!prefix features do not currently agree with yaml

!recommend specifying animacy near stem and as inflectional feature in plurals/obviatives
!small redundancy, but spares memory and allows agreement checking if not inflected
!memory not spared, because you might not want to accept the wrong gendered plural marker (though it does happen under certain discursive contexts)

LEXICON N_FIRST_PERSON_FLAG 
@P.Person.1@    NOUNS ;

LEXICON N_SECOND_PERSON_FLAG 
@P.Person.2@    NOUNS ;

LEXICON N_THIRD_PERSON_FLAG 
@P.Person.3@    NOUNS ;

LEXICON N_X_PERSON_FLAG 
@P.Person.X@    NOUNS ;

LEXICON N_CON_DIM 
+Con:enh    N_POS_THEME_GATEWAY ;
+Dim:ens    N_POS_THEME_GATEWAY ;
            N_POS_THEME_GATEWAY ;

LEXICON N_POS_THEME_GATEWAY 
!go to N_POS_THEME only if you have person
!go to N_PEJ is legit alternative
@R.Person@  N_POS_THEME ;
            N_PEJ ;

LEXICON N_POS_THEME
!optionality of +Pos instantiated in N_POS_THEME_GATEWAY
!entrance requires a person flag, person flags are not forced to enter
+Pos:im N_PEJ ;

LEXICON N_PEJ 
+Pej:ish    N_POS_NUM_GATEWAY ;
            N_POS_NUM_GATEWAY ;

LEXICON N_POS_NUM_GATEWAY 
@R.Person.1@            N_POS_NUM_1PL_21 ;
@R.Person.2@            N_POS_NUM_1PL_21 ;
@R.Person@@D.Person.1@ N_POS_NUM_2PL_3PL ; !indefinite plural poss can enter
@R.Person.3@            N_POS_NUM_3OBV ; !should indefinite obviatives be permitted? 
                        N_MOOD ;

!optionality of possessor number +Pos instantiated in GATEWAY
!entrance requires a person flag, person flags are not forced to enter

!should there be a nomenclature for the descendants of gateways?

LEXICON N_POS_NUM_1PL_21 
<["+1Pl"|"+21"] .x. [i n a a n]>   N_MOOD ; !yaml currently excludes second person prefix, though this is used for first person inclusive
!the following might be intuitive, but it is wrong
!it will match a "+" followed by a multicharacter "1Pl" ... which doesn't exist
!further, to make an output, you need to cross the fsas. 
!   [a|b] .x. [c|d] -> a:c, b:d
!   [a|b] .x. [c] -> a:c, b:c
!<"+"[1Pl|21]>:inaan   N_MOOD ; !yaml currently excludes second person prefix, though this is used for first person inclusive
!style on regexes for features
!yaml still has +Pos1Pl, +PosPl, +PosObv

LEXICON N_POS_NUM_2PL_3PL 
<["+2Pl"|"+3Pl"] .x. [i w a a]>     N_MOOD ;

LEXICON N_POS_NUM_3OBV 
+3Obv:ini              N_MOOD ;

LEXICON N_MOOD 
+Pret:iban  N_NUM_GATEWAY ;
+VocPl:idig # ; !Long feat name, variation abandoned, are there inanimate vocative plurals (-idin)?
            N_NUM_GATEWAY ;

LEXICON N_NUM_GATEWAY 
@R.Animate.1@   NA_NUM ;
@R.Animate.0@   NI_NUM ;
+Obv:an              # ;
+Loc:ing             # ;
                # ;
!intended: take one or none  of obv, loc, pl suffs, but no more
LEXICON NA_NUM 
+Pl:ag # ;

LEXICON NI_NUM 
+Pl:an # ;

