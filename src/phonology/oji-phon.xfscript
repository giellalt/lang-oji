
! Morphophonology for Ojibwe
! ==========================



! Usage for checking this file without a lexicon:
! cd main/langs/oji/src/phonology
! make
! echo wordform | lookup oji-phon.lookup.xfst
! (or eventually 
! just the lookup command, and feed forms, or 
! cat file |  lookup oji-phon.lookup.xfst

! Note:
! Letter should be written with space inbetween
! Insertion rules (0 -> a) must be written [..] -> a)
! Empty context is unstated (writing || _ ; is illegal)
! Remember to declare rules in the composing secion at the end
! Rules are ordered in the composing section
! Remember the purpose of the file: to map lexicon via morphophonology to wordform

! Definitions

define Sigma [ a | e | i | o | a a | i i | o o |
p | t | k | %' |
b | d | g |
ch | j |
s | z | s h | z h |
m | n |
w | y |
%> | %^ !standard m-boundary, person pre-stem boundary
] ;

!###############
!Natural Classes
!###############

define VShort [ a | i | o ] ;

define VLong [ a a | e | i i | o o ] ;

define NasalTrig [ n [ h | s | y ] ] ;

define VNas [ VLong ( NasalTrig ) ] ;

define Vowel [ VShort | VLong | VNas ] ;

define Obs [ p | t | k | %' | b | d | g | c h | j | s | z | s h | z h ] ;

define Nas [ m | n ] ;

define Glide [ w | y ] ;

define Son [ Nas | Glide ] ;

define Cons [ Son | Obs ] ;

define NonConsShort [ Glide | VShort ] ;

define TDS [ t | d | s ] ; !targets of palatalization

define MorphemeBound [ %> | %^ | %< ] ;

!###############
!Rules
!###############

!this rule bled itself historically
!never applied in sub-minimal words
!minimality was tricky.
!   SS sequences permitted no deletion (glides or vowels)
!       SSG could have been optimal iambic foot if codas contributed morae!
!       so there might have been a nuanced notion of minimality
!   SL sequences permitted glide deletion
!   L sequences permitted glide deletion

define ApocopeHist NonConsShort -> 0 || _ .#. ;

!There is an alternative analysis of apocope, but it is hairy
!need to specify inflected verb
!is bled by [n] deletion in Odawa (Piggott 1974)

define ApocopeMod VShort -> 0 || _ .#. ;

!Part of a conspiracy against vowel hiatus
!Also possible for V1 in V1V2 to delete
!Not totally confident that environment covers all cases (necessary, not sufficient)

define HiatusDel VShort -> 0 || VLong _ ;

define HiatusPre [..] -> d || %^ _ Vowel ;

!as Vowel is currently defined /aansaa/ will trigger epenthesis
!Miikka!

define HiatusEpen [..] -> %' || Vowel _ Vowel ;

!The weirdo rule
!traceable to VwV becoming oo in Proto-Alg (Goddard 1972?)
!   o<*w@ 
!but there must have been some sort of reanalysis
!otherwise why coalesce when there is now an epenthetic d?
!This is the other rule that refers to the prefix-stem boundary

define OLengthen o -> o o || %^ _ ; 

!Kaye 1973 (71?) As I recall there was a morpheme boundary in at least one rule

define Coalescence1 a w i -> o o ;

define Coalsecence2 a w i -> a a || _ k ;

!-----
!controversial abstract phonology
!some of these rules use, or should use, segments that are not yet in Sigma
!-----

!yarg! how to just specify a feature that changes and keep the rest
!want t->ch, d->j, s->sh
!s->sh is pretty rare, counter-fed by i<*e. Valentine transcribes with S
!does non-alternating /s/ come from a different historical source?
!or were they all before *e?

!S not in Sigma
define Palatalization1 TDS -> [ c h | j | s h ] || _ i ;

!now we get to some fun stuff.
!n<*l alternates with zh, but n<*n does not
!i<*i triggers the alternation, but i<*e does not
!Bloomfield transcribed the palatalizing ns as "N". Valentine too.
!no special transcription for i<*e since it didn't alternate in the daughter langs
!we can recapitulate history here, or have a lot of lex exceptions
!Valentine states that only n<*l<*T alternates
!Kiparsky-Piggott in 70's stated that n<*l<*T and n<*l<*l alternate

!l not in Sigma
define Palatalization2Hist l -> z h || _ i ;

define Palatalization2Mod n -> z h || _ i ;

!i%<e not in Sigma
!is this how to define in all environments?

define LowShortRaising i %< e -> i  ;

!What happened to *sy?
define Palatalization3 [d | t | l ] y -> [ j | c h | z h ] ; 

!-----
!end controversial abstract phonology
!-----

!Valentine 2001 'linking vowel'. There are others.
!The discussion on 339 almost makes it sound like [i] epenthesis follows syncope
!But I don't think that is right (and it can be read as it preceding too)

define LowEpenthesis 0 -> a || Nasal _ Nasal ;

!###############
!Composing the grammar
!###############


read regex [ 
    ApocopeHist       ! NonConsShort -> 0 || _ .#. ;
.o. ApocopeMod        ! VShort -> 0 || _ .#. ;
.o. HiatusDel         ! VShort -> 0 || VLong _ ;
.o. HiatusPre         ! 0 -> d || %^ _ Vowel ;
.o. HiatusEpen        ! 0 -> ' || Vowel _ Vowel ;
.o. OLengthen         ! o -> o o || %^ _ ; 
.o. Coalescence1      ! a w i -> o o || _ ;
.o. Coalsecence2      ! a w i -> a a || _ k ;
.o. Palatalization1   ! TDS -> [ ch | j | sh ] || _ i ;
.o. Palatalization2Hist  
.o. Palatalization2Mod
.o. LowShortRaising  
.o. Palatalization3  
.o. LowEpenthesis    

] ;


  
 
